default:
    just --list

rust_version := `rustc -vV | grep '^release: ' | cut -c 10-`
tag := "harmlesstech/rust"
pwd := `pwd`

alias b := build
alias r := run
alias t := test

arch:
    @echo "amd64, arm64, riscv64, ppc64le, s390x, 386, mips64le, mips64, arm/v7"

distro:
    @echo "alpine, debian"

# Builds an image
build-img ARCH TAG:
   docker build --no-cache --pull=true --platform=linux/{{ARCH}} -t {{tag}}:{{TAG}} .
# --no-cache --progress plain

build DISTRO ARCH:
    ./gen.py {{ARCH}} {{DISTRO}}:latest tag {{rust_version}}
    just build-img {{ARCH}} {{DISTRO}}

build-all:
    just build alpine amd64
    just build alpine arm64

    just build debian amd64
    just build debian arm64
    just build debian ppc64le
    just build debian s390x
    just build debian 386
    just build debian arm/v7

run DISTRO ARCH: _cargo-reg
    docker run -it --rm --pull=never --platform=linux/{{ARCH}} \
    -v {{pwd}}/cr.tmp:/usr/local/cargo/registry \
    {{tag}}:{{DISTRO}} \
    sh

rb DISTRO ARCH:
    just b {{DISTRO}} {{ARCH}}
    just r {{DISTRO}} {{ARCH}}

test DISTRO ARCH: _cargo-reg
    docker run -it --rm --pull=never --platform=linux/{{ARCH}} \
    -v {{pwd}}/test.sh:/test.sh \
    -v {{pwd}}/cr.tmp:/usr/local/cargo/registry \
    {{tag}}:{{DISTRO}} \
    /test.sh

test-all:
    just test alpine amd64
    just test alpine arm64

    just test debian amd64
    just test debian arm64
    just test debian ppc64le
    just test debian s390x
    just test debian 386
    just test debian arm/v7

bt-all: build-all test-all

_cargo-reg:
    mkdir -p cr.tmp

### Alpine
### AmazonLinux
### Debian
### OracleLinux
### Photon
### RockyLinux
### Ubuntu
